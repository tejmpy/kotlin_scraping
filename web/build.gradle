plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id 'org.springframework.boot' version "$springBootVersion"
    id 'org.flywaydb.flyway' version "$flywayVersion"
    id 'co.uzzu.dotenv.gradle' version '1.1.0'
    id 'org.jlleitschuh.gradle.ktlint' version '10.1.0'
    id 'idea'
}

configurations {
    flywayMigration
}

dependencies {
    flywayMigration "mysql:mysql-connector-java:$mysqlConnectorVersion"
}

flyway {
    url = "jdbc:mysql://dbserver:3306/${env.DB_MAIN_DATABASE.value}?useUnicode=true"
    user = env.DB_USER.value
    password = env.DB_PASSWORD.value
    locations = ['filesystem:db/migration']
    configurations = ['compileClasspath', 'flywayMigration']
}

allprojects {
    apply plugin: 'org.flywaydb.flyway'
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    repositories {
        mavenCentral()
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = targetCompatibility
            freeCompilerArgs = ['-Xjsr305=strict']
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = targetCompatibility
            freeCompilerArgs = ['-Xjsr305=strict']
        }
    }

    dependencies {
        // Kotlin
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        // Spring Boot
        implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
        implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
        implementation "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"
        testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

        // Jackson
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonVersion"

        // Flyway
        implementation "org.flywaydb:flyway-core:$flywayVersion"

        // Dotenv
        implementation "me.paulschwarz:spring-dotenv:$dotenvVersion"

        // Exposed
        implementation "org.jetbrains.exposed:exposed-spring-boot-starter:$exposedVersion"

        // DataSource
        runtimeOnly 'com.zaxxer:HikariCP:3.4.3'
        runtimeOnly "mysql:mysql-connector-java:$mysqlConnectorVersion"

        // JSoup
        implementation("org.jsoup:jsoup:1.14.1")

        // Kotest
        testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotestVersion"
    }

    test {
        useJUnitPlatform()
    }
}